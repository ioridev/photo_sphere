# Plot example of entropy-cutoff function


load "@srcdir@/config.gp"


Lower = 0.05
Upper = 0.9
Offset = 5e-3

_Epsilon = 1.0 / 1024.0
Step(X) = X < 0 ? 0 : (X > _Epsilon ? 1 : 1/0)

EntropyCutoffProper(Y, LowerCutoff, UpperCutoff) = \
    Y <= LowerCutoff ? 0.0 : (Y >= UpperCutoff ? 1.0 : Y)

EntropyCutoff(Y, LowerCutoff, UpperCutoff) = \
    Step(Y - LowerCutoff) * Step(UpperCutoff - Y) * \
    EntropyCutoffProper(Y, LowerCutoff, UpperCutoff) + \
    Step(Y - UpperCutoff)


set grid
set key right bottom
set samples 1023
set xlabel "Y"
set xtics 0.2
set ylabel "EC"
set yrange [-0.1:1.1]
set ytics 0.2


set terminal unknown
plot [Y = 0:1] Y + Offset, EntropyCutoff(Y, Lower, Upper) - Offset

set output "entropy-cutoff.txt"
set terminal dumb Dumb_Width Dumb_Height enhanced
replot

set output "entropy-cutoff.eps"
set terminal postscript eps enhanced
replot

set output "entropy-cutoff.svg"
set terminal svg size Svg_Width, Svg_Height dynamic enhanced
replot

# Newer Gnuplots have a "pdf" terminal.
set output "| ps2pdf -dEPSCrop - entropy-cutoff.pdf"
set terminal postscript eps size Pdf_Width, Pdf_Height enhanced
replot

set output "@RASTER_DIR@/entropy-cutoff.png"
set terminal png transparent size Png_Width, Png_Height enhanced
replot
