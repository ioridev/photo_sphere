# Plot example of exposure-cutoff function


load "@srcdir@/config.gp"


Lower = 0.05
Upper = 0.97
Mu = 0.5
Sigma = 0.2


_Epsilon = 1.0 / 1024.0
Step(X) = X < 0 ? 0 : (X > _Epsilon ? 1 : 1/0)

Gaussian(Y, Mu, Sigma) = exp(-0.5 * ((Y - Mu) / Sigma)**2)

ExposureCutoffProper(Y, LowerCutoff, UpperCutoff) = \
    Y <= LowerCutoff ? 0 : (Y >= UpperCutoff ? 0 : Gaussian(Y, Mu, Sigma))

ExposureCutoff(Y, LowerCutoff, UpperCutoff) = \
    Step(Y - LowerCutoff) * \
    Step(UpperCutoff - Y) * \
    ExposureCutoffProper(Y, LowerCutoff, UpperCutoff)


set grid
set key right bottom
set samples 1023
set xlabel "Y"
set xtics 0.2
#--set ylabel "weight"
set yrange [-0.1:1.1]
set ytics 0.2


set terminal unknown
plot [Y = 0:1] ExposureCutoff(Y, Lower, Upper)

set output "exposure-cutoff.txt"
set terminal dumb Dumb_Width Dumb_Height enhanced
replot

set output "exposure-cutoff.eps"
set terminal postscript eps enhanced
replot

set output "exposure-cutoff.svg"
set terminal svg size Svg_Width, Svg_Height dynamic enhanced
replot

# Newer Gnuplots have a "pdf" terminal.
set output "| ps2pdf -dEPSCrop - exposure-cutoff.pdf"
set terminal postscript eps size Pdf_Width, Pdf_Height enhanced
replot

set output "@RASTER_DIR@/exposure-cutoff.png"
set terminal png transparent size Png_Width, Png_Height enhanced
replot
