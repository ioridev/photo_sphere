# Plot Gauss curve with default parameters of Enfuse


load "@srcdir@/config.gp"


Mu = 0.5
Sigma = 0.2


Gaussian(Y) = exp(-0.5 * ((Y - Mu) / Sigma)**2)


set samples 1023
set xlabel "Y"
set xtics 0.2
set ytics 0.2


set terminal unknown
plot [Y = 0:1] Gaussian(Y)

set output "gaussian.txt"
set terminal dumb Dumb_Width Dumb_Height enhanced
replot

set output "gaussian.eps"
set terminal postscript eps enhanced
replot

set output "gaussian.svg"
set terminal svg size Svg_Width, Svg_Height dynamic enhanced
replot

# Newer Gnuplots have a "pdf" terminal.
set output "| ps2pdf -dEPSCrop - gaussian.pdf"
set terminal postscript eps size Pdf_Width, Pdf_Height enhanced
replot

set output "@RASTER_DIR@/gaussian.png"
set terminal png transparent size Png_Width, Png_Height enhanced
replot
