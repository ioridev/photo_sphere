# Plot radial component of Laplacian-of-Gaussian


load "@srcdir@/config.gp"


Sigma = 0.5


LaplacianOfGaussian(R, Sigma) = \
    ((R**2 / (2.0 * Sigma**2)) - 1.0) * \
    exp(-(R**2 / (2.0 * Sigma**2))) / \
    (pi * Sigma**4)


set key bottom right
set grid
set samples 1023
set xlabel "R"
#set xtics 0.2
set ylabel "k(R)"
#set ytics 0.2


set terminal unknown
plot [R = 0:2] LaplacianOfGaussian(R, Sigma)

set output "laplacian-of-gaussian.txt"
set terminal dumb Dumb_Width Dumb_Height enhanced
replot

set output "laplacian-of-gaussian.eps"
set terminal postscript eps enhanced
replot

set output "laplacian-of-gaussian.svg"
set terminal svg size Svg_Width, Svg_Height dynamic enhanced
replot

# Newer Gnuplots have a "pdf" terminal.
set output "| ps2pdf -dEPSCrop - laplacian-of-gaussian.pdf"
set terminal postscript eps size Pdf_Width, Pdf_Height enhanced
replot

set output "@RASTER_DIR@/laplacian-of-gaussian.png"
set terminal png transparent size Png_Width, Png_Height enhanced
replot
